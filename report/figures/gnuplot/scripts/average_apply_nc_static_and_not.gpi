reset
unset key
y1 = 0.0; y2 = 60.0; y3 = 160.0; y4 = 170.0;
set term postscript eps color
set output 'avg_static_and_not_apply_nodes.eps'

set xtics rotate by -45
set ylabel "Linear avg. per benchmark"
set grid ytics lc rgb "#bbbbbb" lw 1 lt 0

# Plot type
set style data histogram
set style fill solid 1.0

set multiplot
set key off
set border 1+2+8
set xtics nomirror
set ytics nomirror
set lmargin at screen 0.12
set rmargin at screen 0.95
set bmargin at screen 0.15
set tmargin at screen 0.15 + 0.75 * 0.5

set yrange[y1:y2]
plot 'benchmark_averages.data' using 5 t "Apply Nodes", \
	'' using 33:xticlabels(1) t "Statically Known Apply Nodes"

unset xtics
unset xlabel
unset ylabel
set border 2+4+8
set bmargin at screen 0.15 + 0.75*0.5 + 0.03
set tmargin at screen 0.15 + 0.75 + 0.03

set yrange [y3:y4]
plot 'benchmark_averages.data' using 5 t "Apply Nodes", \
	'' using 33:xticlabels(1) t "Statically Known Apply Nodes"

unset multiplot
